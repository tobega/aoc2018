composer plants
  (<'initial state: '>) { state_string: <~WS> (<WS>*), input_rules: <rules> }

  rule rules: [ <rule>+ ]

  rule rule: { pattern: <'[.#]+'> (<' => '>), result: <'[.#]'> } (<WS>*)
end plants

templates encodeRule
  @: 0;
  $... -> #
  $@ !

   when <'#'> do
     @: ($@ * 2) + 1;

   when <'.'> do
     @: $@ * 2;
end encodeRule

templates reduce&{f:}
  @: $(1);
  $(2..last)... -> @: [ $@, $] -> f;
  $@!
end reduce

processor Plantation
  @: $;

  templates debug
    $@Plantation.state !
  end debug

  templates evolve // should be sink (or really sink&source)
    templates checkAndShift
      when <{locale: <=0>}> do
        { $..., pos: $.pos + 1 } !
      otherwise
        def prev: $;
        { $prev..., newState: $@Plantation.rules($prev.locale) -> \(<='#'> [$prev.newState..., $prev.pos] ! <> $prev.newState !\),
              pos: $prev.pos + 1, locale: (($prev.locale * 2) mod 32) } !
    end checkAndShift

    { pos: $@Plantation.state(1) - 2, i: 1"1", locale: 0"1", newState: [] } -> #

    when <{i: <..$@Plantation.state::length>}?($.pos <=$@Plantation.state($.i)-2>)> do
      def prev: $;
      { $prev..., locale: $.locale + 1, i: $.i + 1 } -> #

    when <{i: <..$@Plantation.state::length>}> do
      $ -> checkAndShift -> #

    when <{locale: <0~..>}> do
      $ -> checkAndShift -> #

    otherwise
      ..|@Plantation: {state: $.newState};
  end evolve

  templates potSum // should be source
      templates sum
        $(1) + $(2) !
      end sum
    $@Plantation.state -> reduce&{f:sum} !
  end potSum

end Plantation

templates makeIndexed
  @: [1..$::length -> '.'];
  $... -> #
  $@!
  when <{rule: <1..>}> do
    @($.rule): $.result;
end makeIndexed

def startState: '$IN::lines -> \($! ' '!\);' -> plants -> { state: [$.state_string...] -> \[i](<'#'> ($i - 1)"pot" !\),
  rules: [ $.input_rules... -> { rule: $.pattern -> encodeRule, result: $.result }] -> makeIndexed};

def part1: $startState -> Plantation;
1..20 -> $part1::evolve -> !VOID
$part1::potSum -> !OUT::write
