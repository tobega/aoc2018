templates max
  @: $it(1)
  $it(2..-1)... -> #
  $@ !
  <$@~..>
    @: $it
end max

processor Game
  @: {head: 1, tail: 1, ring: [1..2*$it -> 0]}
  templates play
    <?($it mod 23 <0>)>
      @Game.ring($@Game.tail-6..$@Game.tail-1): $@Game.ring($@Game.head-5..$@Game.head)...
      def newHead: $@Game.head - 7
      def score: $it + $@Game.ring($newHead)
      @Game.ring($newHead): $@Game.ring($newHead + 1)
      ..|@Game: {head: $newHead, tail: $@Game.tail-6}
      $score !
    <>
      @Game.ring($@Game.head+1): $@Game.ring($@Game.tail)
      @Game.ring($@Game.head+2): $it
      ..|@Game: {head: $@Game.head + 2, tail: $@Game.tail + 1}
      0 !
  end play
end Game

templates game@{players:}
  def ring: $it -> Game
  @: [ 1..$players -> 0 ]
  1..$it -> #
  $@ -> max !
  <>
    def player: (($it - 1) mod $players) + 1
    @($player): $it -> $ring::play -> $it + $@($player)
end game

1618 -> game@{players: 10} -> !OUT::write
