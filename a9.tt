templates max
  @: $(1);
  $(2..-1)... -> #
  $@ !
  <$@~..>
    @: $;
end max

processor Game
  @: {head: 1, tail: 1, ring: [1..2*$ -> 0]};
  templates play
    <?($ mod 23 <=0>)>
      @Game.ring($@Game.tail-6..$@Game.tail-1): $@Game.ring($@Game.head-5..$@Game.head)...;
      def newHead: $@Game.head - 7;
      def score: $ + $@Game.ring($newHead);
      @Game.ring($newHead): $@Game.ring($newHead + 1);
      ..|@Game: {head: $newHead, tail: $@Game.tail-6};
      $score !
    <>
      @Game.ring($@Game.head+1): $@Game.ring($@Game.tail);
      @Game.ring($@Game.head+2): $;
      ..|@Game: {head: $@Game.head + 2, tail: $@Game.tail + 1};
      0 !
  end play
end Game

templates game@{players:}
  def ring: $ -> Game;
  @: [ 1..$players -> 0 ];
  1..$ -> #
  $@ -> max !
  <>
    def player: (($ - 1) mod $players) + 1;
    @($player): $ -> ring::play -> $ + $@($player);
end game

1618 -> game@{players: 10} -> !OUT::write
